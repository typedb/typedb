insert
#string, long, double, boolean

"entity" isa entity-type, is-abstract
    has-resource name;
"place" sub entity;
"organization" sub entity;
"tag" sub entity;
"category" sub entity;
"forum" sub entity;

# generic resources

# a generic relationship for resources of entity
"entity-resource-owner" isa role-type;
"entity-resource-value" isa role-type;
"entity-resource" isa relation-type, has-role entity-resource-value, has-role entity-resource-owner;
entity plays-role entity-resource-owner;

# a generic relationship for resources of relation
"relation" isa relation-type, is-abstract;
"relation-resource-owner" isa role-type;
"relation-resource-value" isa role-type;
"relation-resource" isa relation-type, has-role relation-resource-value, has-role relation-resource-owner;
relation plays-role relation-resource-owner;

"subject-location" sub location;
"located-subject" isa role-type;
"resides" isa relation-type, has-role subject-location, has-role located-subject;

# place
"continent" sub place;
"country" sub place;
"city" sub place;


"location" isa role-type, is-abstract;

"member-location" sub location;
"container-location" sub location;
"sublocate" isa relation-type, has-role member-location, has-role container-location;
place plays-role member-location, plays-role container-location, plays-role subject-location;

# organization
"university" sub organization;
"company" sub organization;
organization plays-role located-subject;

# person
"person" isa entity-type;

person
    has-resource name,
    has-resource firstname,
    has-resource lastname,
    has-resource gender,
    has-resource birthday,
    has-resource email,
    has-resource age;

"name" isa resource-type, datatype string;
"firstname" isa resource-type, datatype string;
"lastname" isa resource-type, datatype string;
"gender" isa resource-type, datatype string;
"birthday" isa resource-type, datatype string;
"email" isa resource-type, datatype string;
"age" isa resource-type, datatype long;

person plays-role located-subject;

"acquaintance" isa role-type, is-abstract;
"acquaintance1" sub acquaintance;
"acquaintance2" sub acquaintance;
"knows" sub relation, has-role acquaintance1, has-role acquaintance2;
person plays-role acquaintance1, plays-role acquaintance2;

"student" isa role-type;
"enrolled-university" isa role-type;
"attends" isa relation-type, has-role student, has-role enrolled-university;
person plays-role student;
university plays-role enrolled-university;

"employee" isa role-type;
"employer" isa role-type;
"employment" isa relation-type, has-role employee, has-role employer;
person plays-role employee;
company plays-role employer;

"class-year" isa resource-type, datatype string;
"attends-resource-value" isa role-type;
"attends-resource-owner" isa role-type;
"attends-resource" isa relation-type, has-role attends-resource-value, has-role attends-resource-owner;
attends plays-role attends-resource-owner;
class-year plays-role attends-resource-value;

"employment-startdate" isa resource-type, datatype string;
"employment-resource-value" isa role-type;
"employment-resource-owner" isa role-type;
"employment-resource" isa relation-type, has-role employment-resource-value, has-role employment-resource-owner;
employment plays-role employment-resource-owner;
employment-startdate plays-role employment-resource-value;

# message
"message" isa entity-type, is-abstract;

"message-resource-owner" isa role-type;
"message-resource-value" isa role-type;
"message-resource" isa relation-type, has-role message-resource-value, has-role message-resource-owner;
message plays-role message-resource-owner;

"content" isa resource-type, datatype string, plays-role message-resource-value;
"length" isa resource-type, datatype string, plays-role message-resource-value;

"liked" isa role-type;
"liker" isa role-type;
"likes" sub relation, has-role liked, has-role liker;
message plays-role liked;
person plays-role liker;

"written" isa role-type;
"writer" isa role-type;
"writes" isa relation-type, has-role written, has-role writer;
message plays-role written;
person plays-role writer;

message plays-role located-subject;

"comment" sub message;

"reply-owner" isa role-type;
"reply-content" isa role-type;
"reply" isa relation-type, has-role reply-content, has-role reply-owner;
comment plays-role reply-content;
message plays-role reply-owner;

"post" sub message;

"post-resource-owner" isa role-type;
"post-resource-value" isa role-type;
"post-resource" isa relation-type, has-role post-resource-owner, has-role post-resource-value;
post plays-role post-resource-owner;

"image-file" isa resource-type, datatype string, plays-role post-resource-value;

# forum
"forum-resource-owner" isa role-type;
"forum-resource-value" isa role-type;
"forum-resource" isa relation-type, has-role forum-resource-value, has-role forum-resource-owner;
forum plays-role forum-resource-owner;

"moderator" isa role-type;
"moderated" isa role-type;
"moderates" isa relation-type, has-role moderator, has-role moderated;
person plays-role moderator;
forum plays-role moderated;

"forum-member" isa role-type;
"membered-forum" isa role-type;
"membership" sub relation, has-role forum-member, has-role membered-forum;
person plays-role forum-member;
forum plays-role membered-forum;

"post-container" isa role-type;
"contained-post" isa role-type;
"containing" isa relation-type, has-role post-container, has-role contained-post;
post plays-role contained-post;
forum plays-role post-container;

# tag
"subject-tag" isa role-type;
"tagged-subject" isa role-type;
"tagging" isa relation-type, has-role tagged-subject, has-role subject-tag;
tag plays-role subject-tag;
forum plays-role tagged-subject;
message plays-role tagged-subject;
person plays-role tagged-subject;

# tag class
"group" isa role-type, is-abstract;
"subgroup" sub group;
"supergroup" sub group;
"subgrouping" isa relation-type, has-role subgroup, has-role supergroup;
category plays-role subgroup, plays-role supergroup;

"grouped-tag" isa role-type;
"tag-group" isa role-type;
"grouping" isa relation-type, has-role grouped-tag, has-role tag-group;
tag plays-role grouped-tag;
category plays-role tag-group;
