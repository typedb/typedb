insert
#string, long, double, boolean

"entity2" sub entity, is-abstract
    has-resource name;
"place" sub entity2;
"organization" sub entity2;
"tag" sub entity2;
"category" sub entity2;
"forum" sub entity2;

# generic resources

# a generic relationship for resources of entity
"entity-resource-owner" sub role;
"entity-resource-value" sub role;
"entity-resource" sub relation, has-role entity-resource-value, has-role entity-resource-owner;
entity2 plays-role entity-resource-owner;

# a generic relationship for resources of relation
relation2 sub relation, is-abstract;
"relation-resource-owner" sub role;
"relation-resource-value" sub role;
"relation-resource" sub relation, has-role relation-resource-value, has-role relation-resource-owner;
relation2 plays-role relation-resource-owner;

"subject-location" sub location;
"located-subject" sub role;
"resides" sub relation2, has-role subject-location, has-role located-subject;

# place
"continent" sub place;
"country" sub place;
"city" sub place;


"location" sub role, is-abstract;

"member-location" sub location;
"container-location" sub location;
"sublocate" sub relation2, has-role member-location, has-role container-location;
place plays-role member-location, plays-role container-location, plays-role subject-location;

# organization
"university" sub organization;
"company" sub organization;
organization plays-role located-subject;

# person
"person" sub entity2;

person
    has-resource name,
    has-resource firstname,
    has-resource lastname,
    has-resource gender,
    has-resource birthday,
    has-resource email,
    has-resource age;

"name" sub resource, datatype string;
"firstname" sub resource, datatype string;
"lastname" sub resource, datatype string;
"gender" sub resource, datatype string;
"birthday" sub resource, datatype string;
"email" sub resource, datatype string;
"age" sub resource, datatype long;

person plays-role located-subject;

"acquaintance" sub role, is-abstract;
"acquaintance1" sub acquaintance;
"acquaintance2" sub acquaintance;
"knows" sub relation2, has-role acquaintance1, has-role acquaintance2;
person plays-role acquaintance1, plays-role acquaintance2;

"student" sub role;
"enrolled-university" sub role;
"attends" sub relation2, has-role student, has-role enrolled-university;
person plays-role student;
university plays-role enrolled-university;

"employee" sub role;
"employer" sub role;
"employment" sub relation2, has-role employee, has-role employer;
person plays-role employee;
company plays-role employer;

"class-year" sub resource, datatype string;
"attends-resource-value" sub role;
"attends-resource-owner" sub role;
"attends-resource" sub relation2, has-role attends-resource-value, has-role attends-resource-owner;
attends plays-role attends-resource-owner;
class-year plays-role attends-resource-value;

"employment-startdate" sub resource, datatype string;
"employment-resource-value" sub role;
"employment-resource-owner" sub role;
"employment-resource" sub relation2, has-role employment-resource-value, has-role employment-resource-owner;
employment plays-role employment-resource-owner;
employment-startdate plays-role employment-resource-value;

# message
"message" sub entity2, is-abstract;

"message-resource-owner" sub role;
"message-resource-value" sub role;
"message-resource" sub relation2, has-role message-resource-value, has-role message-resource-owner;
message plays-role message-resource-owner;

"content" sub resource, datatype string, plays-role message-resource-value;
"length" sub resource, datatype string, plays-role message-resource-value;

"liked" sub role;
"liker" sub role;
"likes" sub relation2, has-role liked, has-role liker;
message plays-role liked;
person plays-role liker;

"written" sub role;
"writer" sub role;
"writes" sub relation2, has-role written, has-role writer;
message plays-role written;
person plays-role writer;

message plays-role located-subject;

"comment" sub message;

"reply-owner" sub role;
"reply-content" sub role;
"reply" sub relation2, has-role reply-content, has-role reply-owner;
comment plays-role reply-content;
message plays-role reply-owner;

"post" sub message;

"post-resource-owner" sub role;
"post-resource-value" sub role;
"post-resource" sub relation2, has-role post-resource-owner, has-role post-resource-value;
post plays-role post-resource-owner;

"image-file" sub resource, datatype string, plays-role post-resource-value;

# forum
"forum-resource-owner" sub role;
"forum-resource-value" sub role;
"forum-resource" sub relation2, has-role forum-resource-value, has-role forum-resource-owner;
forum plays-role forum-resource-owner;

"moderator" sub role;
"moderated" sub role;
"moderates" sub relation2, has-role moderator, has-role moderated;
person plays-role moderator;
forum plays-role moderated;

"forum-member" sub role;
"membered-forum" sub role;
"membership" sub relation2, has-role forum-member, has-role membered-forum;
person plays-role forum-member;
forum plays-role membered-forum;

"post-container" sub role;
"contained-post" sub role;
"containing" sub relation2, has-role post-container, has-role contained-post;
post plays-role contained-post;
forum plays-role post-container;

# tag
"subject-tag" sub role;
"tagged-subject" sub role;
"tagging" sub relation2, has-role tagged-subject, has-role subject-tag;
tag plays-role subject-tag;
forum plays-role tagged-subject;
message plays-role tagged-subject;
person plays-role tagged-subject;

# tag class
"group" sub role, is-abstract;
"subgroup" sub group;
"supergroup" sub group;
"subgrouping" sub relation2, has-role subgroup, has-role supergroup;
category plays-role subgroup, plays-role supergroup;

"grouped-tag" sub role;
"tag-group" sub role;
"grouping" sub relation2, has-role grouped-tag, has-role tag-group;
tag plays-role grouped-tag;
category plays-role tag-group;
