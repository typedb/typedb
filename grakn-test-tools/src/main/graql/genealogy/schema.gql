define

document sub entity
    plays evidence
    has identifier
    has document-location
    has document-type
    has notes;
document-location sub attribute datatype string;
document-type sub attribute datatype string;

notes sub attribute datatype string;

conclusion-evidence sub relationship
    relates evidence
    relates conclusion;
evidence sub role;
conclusion sub role;

### Events are inferred and supported by documents

event sub entity
    is-abstract
    plays conclusion
    plays happening
    has degree
    has confidence
    has "date"
    has identifier
    has notes;

# This is needed otherwise reasoner complains about the missing attribute if it has not been persisted
degree sub attribute datatype long;
# The following links events to people, specifying the role of the person in the event

event-protagonist sub relationship
    relates happening
    relates protagonist
    has event-role;
happening sub role;
protagonist sub role;
event-role sub attribute datatype string;

wedding sub event;
funeral sub event
    has birth-date;
christening sub event
    has death-date;

birth sub event
    has firstname
    has middlename
    has surname
    has gender
    has birth-date;
child-identifier sub identifier;
parent-identifier sub identifier;

death sub event
    has death-date;

"date" sub attribute datatype date;

#confidence level: low-mid-high
confidence sub attribute datatype string;

### Genealogy tree: this is inferred from the events

person sub entity
    plays protagonist
    plays spouse
    plays parent
    plays child
    plays sibling
    plays grandparent
    plays grandchild
    plays cousin
    plays child-in-law
    plays parent-in-law
    has gender
    has birth-date
    has death-date
    has identifier
    has firstname
    has middlename
    has surname;

gender sub attribute datatype string;
birth-date sub "date";
death-date sub "date";
name sub attribute datatype string;
firstname sub name;
middlename sub name;
surname sub name;
identifier sub attribute datatype string;

relatives sub relationship
    is-abstract;

marriage sub relatives
    relates spouse
    relates husband
    relates wife
    has "date";
spouse sub role;
husband sub spouse;
wife sub spouse;

parentship sub relatives
    relates parent
    relates mother
    relates father
    relates child
    relates son
    relates daughter;
parent sub role;
mother sub parent;
father sub parent;
child sub role;
son sub child;
daughter sub child;

### The following will be added by reasoner
siblings sub relatives
    relates sibling;
sibling sub role;

grandparentship sub relatives
    relates grandparent
    relates grandfather
    relates grandmother
    relates grandchild
    relates granddaughter
    relates grandson;
grandparent sub role;
grandfather sub grandparent;
grandmother sub grandparent;
grandchild sub role;
grandson sub grandchild;
granddaughter sub grandchild;

cousins sub relatives
    relates cousin;
cousin sub role;

in-laws sub relatives
    relates child-in-law
    relates son-in-law
    relates daughter-in-law
    relates parent-in-law
    relates mother-in-law
    relates father-in-law;
child-in-law sub role;
son-in-law sub child-in-law;
daughter-in-law sub child-in-law;
parent-in-law sub role;
mother-in-law sub parent-in-law;
father-in-law sub parent-in-law;
