define

#############################################################################
#                                                                           #
#         Rules to make events with many evidences high confidence          #
#                                                                           #
#############################################################################

#makeHighDegreeEventsHighConfidence sub rule
#when
#{$e isa event has degree > 2;
#}
#then
#{$f has confidence "high";};


#############################################################################
#                                                                           #
# Rules to add personal data from high confidence events to person entities #
#                                                                           #
#############################################################################

 materializePeopleNames sub rule
 when
 {$b isa birth has confidence "high" has firstname $name;
 $rel (happening: $b, protagonist: $p) isa event-protagonist;
 $rel has event-role "newborn";}
 then
 {$p has firstname $name;};

 materializePeopleMiddleNames sub rule
 when
 {$b isa birth has confidence "high" has middlename $name;
 $rel (happening: $b, protagonist: $p) isa event-protagonist;
 $rel has event-role "newborn";}
 then
 {$p has middlename $name;};

 materializePeopleSurnames sub rule
 when
 {$b isa birth has confidence "high" has surname $name;
 $rel (happening: $b, protagonist: $p) isa event-protagonist;
 $rel has event-role "newborn";}
 then
 {$p has surname $name;};

 materializePeopleGender sub rule
 when
 {$b isa birth has confidence "high" has gender $gender;
 $rel (happening: $b, protagonist: $p) isa event-protagonist;
 $rel has event-role "newborn";}
 then
 {$p has gender $gender;};

 materializePeopleBirthDates sub rule
 when
 {$b isa birth has confidence "high" has birth-date $date;
 $rel (happening: $b, protagonist: $p) isa event-protagonist;
 $rel has event-role "newborn";}
 then
 {$p has birth-date $date;};

 materializePeopleDeathDates sub rule
 when
 {$b isa death has confidence "high" has death-date $date;
 $rel (happening: $b, protagonist: $p);
 $rel isa event-protagonist has event-role "deceased";}
 then
 {$p has death-date $date;};

#############################################################################
#                                                                           #
#     Rules to retrieve parentships from high confidence birth events       #
#                                                                           #
#############################################################################

materializeParentChildRelations sub rule
when
{
$b isa birth has confidence "high";
$rel1 (happening: $b, protagonist: $p) isa event-protagonist;
$rel1 has event-role "parent";
$rel2 (happening: $b, protagonist: $c) isa event-protagonist;
$rel2 has event-role "newborn";}
then
{(parent: $p, child: $c) isa parentship;};

#############################################################################
#                                                                           #
#     Rules to retrieve marriages from high confidence wedding events       #
#                                                                           #
#############################################################################

materializeMarriagesFromWeddings sub rule
when
{$w isa wedding has confidence "high";
$rel1 (happening: $w, protagonist: $s1) isa event-protagonist;
$rel1 has event-role "spouse";
$rel2 (happening: $w, protagonist: $s2) isa event-protagonist;
$rel2 has event-role "spouse";
$s1 != $s2;
}
then
{(spouse: $s1, spouse: $s2) isa marriage;};


#############################################################################
#                                                                           #
#     Rules to genderize relationships      #
#                                                                           #
#############################################################################

rule-1 sub rule
when
{(grandparent: $x, grandchild: $y) isa grandparentship;
$x has gender "female";
}
then
{(grandmother: $x) isa grandparentship;};

rule-2 sub rule
when
{(grandparent: $x, grandchild: $y) isa grandparentship;
$x has gender "male";
}
then
{(grandfather: $x) isa grandparentship;};

rule-3 sub rule
when
{(grandchild: $x, grandparent: $y) isa grandparentship;
$x has gender "male";
}
then
{(grandson: $x) isa grandparentship;};

rule-4 sub rule
when
{(grandchild: $x, grandparent: $y) isa grandparentship;
$x has gender "female";
}
then
{(granddaughter: $x) isa grandparentship;};

rule-5 sub rule
when
{(parent: $x, child: $y) isa parentship;
$x has gender "female";
}
then
{(mother: $x) isa parentship;};

rule-6 sub rule
when
{(parent: $x, child: $y) isa parentship;
$x has gender "male";
}
then
{(father: $x) isa parentship;};

rule-7 sub rule
when
{(child: $x, parent: $y) isa parentship;
$x has gender "female";
}
then
{(daughter: $x) isa parentship;};

rule-8 sub rule
when
{(child: $x, parent: $y) isa parentship;
$x has gender "male";
}
then
{(son: $x) isa parentship;};

rule-9 sub rule
when
{($x, $y) isa marriage;
$x has gender "female";
$y has gender "male";}
then
{(wife: $x, husband: $y) isa marriage;};

rule-10 sub rule
when
{(parent-in-law: $x, child-in-law: $y) isa in-laws;
$x has gender "male";
}
then
{(father-in-law: $x) isa in-laws;};

rule-11 sub rule
when
{(parent-in-law: $x, child-in-law: $y) isa in-laws;
$x has gender "female";
}
then
{(mother-in-law: $x) isa in-laws;};

rule-12 sub rule
when
{(child-in-law: $x, parent-in-law: $y) isa in-laws;
$x has gender "male";
}
then
{(son-in-law: $x) isa in-laws;};

rule-13 sub rule
when
{(child-in-law: $x, parent-in-law: $y) isa in-laws;
$x has gender "female";
}
then
{(daughter-in-law: $x) isa in-laws;};

#############################################################################
#                                                                           #
#     Rules to infer kinships
#                                                                           #
#############################################################################

parentsOfParentsAreGrandparents sub rule
when
{(parent:$p, child: $gc) isa parentship;
(parent: $gp, child: $p) isa parentship;
}
then
{(grandparent: $gp, grandchild: $gc) isa grandparentship;};

peopleWithSameParentsAreSiblings sub rule
when
{(parent: $m, child: $x) isa parentship;
(parent: $m, child: $y) isa parentship;
(parent: $f, child: $x) isa parentship;
(parent: $f, child: $y) isa parentship;
$m has gender "female";
$f has gender "male";
$x != $y;
}
then
{(sibling: $x, sibling: $y) isa siblings;};

peopleWithSiblingsParentsAreCousins sub rule
when
{
(parent: $p, child: $c1) isa parentship;
($p, $p2) isa siblings;
(parent: $p2, child: $c2) isa parentship;
$c1 != $c2;
}
then
{(cousin: $c1, cousin: $c2) isa cousins;};

parentsOfSpousesAreInLaws sub rule
when
{(spouse: $x, spouse: $y) isa marriage;
(parent: $il, child: $y) isa parentship;}
then
{(parent-in-law: $il, child-in-law: $x) isa in-laws;};
