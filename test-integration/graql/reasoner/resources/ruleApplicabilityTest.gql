define

#Roles

# Role hierarchy
#
#              role
#            /      \
#        someRole      anotherRole
#          |
#        subRole
#          |
#     symmetricRole
#

someRole sub role;
subRole sub someRole;
symmetricRole sub subRole;
anotherRole sub role;

#Entities

# Entity hierarchy
#
#                                       entity
#                                          |
#                                    noRoleEntity                                                               anotherNoRoleEntity
#                                          |
#  singleRoleEntity(R1)    anotherSingleRoleEntity(R1)   twoRoleEntity(R2, R3)   threeRoleEntity (R1, R2, R3)
#                               |
#                           anotherTwoRoleEntity(R2)
#                               |
#                           anotherThreeRoleEntity(R3)

noRoleEntity sub entity,
    has name,
    has resource-string,
    has resource-long;

anotherNoRoleEntity sub entity,
    has name;

singleRoleEntity sub noRoleEntity,
    plays someRole;

anotherSingleRoleEntity sub noRoleEntity,
    plays someRole;

anotherTwoRoleEntity sub anotherSingleRoleEntity,
    plays subRole,
    plays symmetricRole;

anotherThreeRoleEntity sub anotherTwoRoleEntity,
    plays anotherRole;

twoRoleEntity sub noRoleEntity,
    plays subRole,
    plays anotherRole;

threeRoleEntity sub noRoleEntity,
    plays someRole,
    plays subRole,
    plays anotherRole;

#Relations

ternary sub relation,
    relates someRole,
    relates subRole,
    relates anotherRole;

reifiable-relation sub relation,
    relates someRole,
    relates subRole,
    plays someRole,
    plays subRole,
    has description;

binary sub reifiable-relation,
    relates someRole,
    relates subRole;

reifying-relation sub relation,
    relates someRole,
    relates subRole,
    relates symmetricRole;

typed-relation sub relation,
    relates someRole,
    relates subRole,
    relates anotherRole,
    has description;

attributed-relation sub relation,
    relates someRole,
    relates subRole,
    relates anotherRole;

#Resources
    resource-string sub attribute, datatype string;
    resource-long sub attribute, datatype long;
    name sub attribute, datatype string;
    description sub attribute, datatype string;

#Rules

#3 instances in total
binary-transitivity sub rule,
    when {
    	(someRole: $x, subRole: $y) isa binary;
    	(someRole: $y, subRole: $z) isa binary;
    },
    then {
    	(someRole: $x, subRole: $z) isa binary;
    };

#1 instance
ternary-rule sub rule,
    when {
    	(someRole: $x, subRole: $y) isa binary;
    	(someRole: $y, subRole: $z) isa binary;
    },
    then {
    	(someRole:$x, subRole:$y, anotherRole: $z) isa ternary;
    };

#1 instance
alternative-ternary-rule sub rule,
    when {
    	(someRole: $x, subRole: $y) isa binary;
    	(someRole: $y, subRole: $z) isa binary;
    },
    then {
    	(someRole:$x, subRole:$y, subRole: $z) isa ternary;
    };

#1 instance
reifiable-chain-rule sub rule,
    when {
    	$r1 (someRole: $x, subRole: $y) isa reifiable-relation;
    	$r2 (someRole: $y, subRole: $z) isa reifiable-relation;
    },
    then {
    	(someRole:$r1, subRole:$r2) isa reifying-relation;
    };

typed-reifying-rule sub rule,
    when {
    	$x isa anotherTwoRoleEntity;
    	$y isa anotherTwoRoleEntity;
    },
    then {
    	(symmetricRole:$x, symmetricRole:$y) isa reifying-relation;
    };

typed-relation-rule sub rule,
    when {
    	$x isa singleRoleEntity;
    	$y isa anotherTwoRoleEntity;
    	$z isa twoRoleEntity;
    },
    then {
    	(someRole:$x, subRole:$y, anotherRole: $z) isa typed-relation;
    };

attributed-relation-string-rule sub rule,
    when {
    	$x isa singleRoleEntity, has resource-string 'someValue';
    	$y isa anotherTwoRoleEntity, has resource-string 'someOtherValue';
    	$z isa twoRoleEntity, has resource-string 'yetAnotherValue';
    },
    then {
    	(someRole:$x, subRole:$y, anotherRole: $z) isa attributed-relation;
    };

attributed-relation-long-rule sub rule,
    when {
    	$x isa singleRoleEntity, has resource-long < -1337;
    	$y isa anotherTwoRoleEntity, has resource-long !== 0;
    	$z isa twoRoleEntity, has resource-long > 1606;
    },
    then {
    	(someRole:$x, subRole:$y, anotherRole: $z) isa attributed-relation;
    };

reifiable-relation-description-rule sub rule,
    when {
        $rel (someRole: $x, subRole: $y) isa reifiable-relation;
    },
    then {
        $rel has description 'reified';
    };

typed-relation-description-rule sub rule,
    when {
        $r1 (someRole:$x, subRole:$y, anotherRole: $z) isa typed-relation;
    },
    then {
        $r1 has description 'reified';
    };

description-rule sub rule,
    when {
        $r has description 'typed';
        $r has description 'reified';
    },
    then {
        $r has description 'typed-reified';
    };

insert

#Data

$x isa anotherNoRoleEntity, has name 'noRoleEntity';

$y isa anotherTwoRoleEntity;
$z isa anotherTwoRoleEntity;
$u isa anotherTwoRoleEntity;

(someRole: $y, subRole: $z) isa binary;
(someRole: $z, subRole: $u) isa binary;