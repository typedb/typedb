# SCHEMA

define

organisation sub entity,
	has name,
	has ticker,
	plays owner,
	plays owned,
	plays risk-subject,
	plays contained,
	plays jv-owned,
	plays jv-owner,
	plays attacked,
	plays managed;
corporate sub organisation;
bank sub corporate;
securities-joint-venture sub corporate;
real-estate-corporate sub corporate;
special-purpose-vehicle sub corporate,
	plays board-member;

obligation sub entity,
	plays contained,
	plays owned;
bond sub obligation,
	has parValue,
	has shares-outstanding,
	has primary-exchange,
	has voting,
	has security-description,
	has isin,
	has issueTicker,
	plays issued;

continent sub entity,
	has name,
	plays container,
	plays risk-subject,
	plays issuer;

country sub continent;

jurisdiction sub country,
	has isoNumeric,
	has isoAlpha,
	plays risk-jurisdiction;

risk-score sub entity,
	has value,
	plays risk-value,
	has risk-level;
war sub risk-score,
	plays individual-risk;
terrorism sub risk-score,
	plays individual-risk;
civil-unrest sub risk-score,
	plays individual-risk;
cyber-crime sub risk-score,
	plays individual-risk;

person sub entity,
	has name,
	plays director,
	plays owner;
man sub person,
	plays husband;
woman sub person,
	plays wife;

board-of-directors sub entity,
	plays company-board,
	plays board-member;

asset sub entity,
	has name,
	plays owned,
	plays contained;

energy-asset sub asset;

attack-campaign sub entity,
	has name,
	plays attacker-campaign,
	plays attack-used,
	plays risk-subject;
DDOS sub entity,
	has ddos-type,
	plays attack-uses;

management sub relation,
	relates company-board,
	relates managed;

mariage sub relation,
	relates wife,
	relates husband;

directorship sub relation,
	relates director,
	relates board-member;

risk-exposure sub relation,
	relates risk-value,
	relates risk-subject;

obligation-issuer sub relation,
	relates issued,
	relates issuer;

risk sub relation,
	has risk-level,
	relates individual-risk,
	relates risk-jurisdiction,
	plays risk-value;

joint-venture sub relation,
	relates jv-owned,
	relates jv-owner;

located-in sub relation,
  relates contained,
  relates container;

owns sub relation,
  relates owner,
  relates owned;

indirect-owns sub owns,
	relates owner,
	relates owned;

cyber-attack sub relation,
	relates attacked,
	relates attacker-campaign;
attack-type sub relation,
	relates attack-used,
	relates attack-uses;

name sub attribute, datatype string;
ticker sub attribute, datatype string;
value sub attribute, datatype double;
risk-level sub attribute, datatype string;
parValue sub attribute, datatype double;
shares-outstanding sub attribute, datatype double;
primary-exchange sub attribute, datatype string;
voting sub attribute, datatype string;
security-description sub attribute, datatype string;
isin sub attribute, datatype string;
issueTicker sub attribute, datatype string;
isoNumeric sub attribute, datatype string;
isoAlpha sub attribute, datatype string;
ddos-type sub attribute, datatype string;

org-asset-jur-risk sub rule,
when {
	$b isa bank;
	$a isa energy-asset;
	$j isa jurisdiction;
	$civil isa civil-unrest;
	$r1 (owner: $b, owned: $a) isa owns;
	$r2 (contained: $a, container: $j) isa located-in;
	$r3 (risk-subject: $j, risk-value: $civil) isa risk-exposure;
}, then {
	(risk-subject: $b, risk-value: $civil) isa risk-exposure;
};

organisation-owns-subsidiary-bond sub rule,
when {
	$c isa organisation; $c2 isa organisation; $c != $c2;
	$c3 isa organisation; $c3 != $c;
	$e isa bond;
	$jv (jv-owned: $c3, jv-owner: $c2, jv-owner: $c) isa joint-venture;
	$r2 (owner: $c3, owned: $e) isa owns;
	},
then {
	(owner: $c, owned: $e) isa indirect-owns;
	};


when-frog-eq-jur-combined-risk2 sub rule,
when {
	$o isa organisation; $e isa bond;
	$j isa jurisdiction;
	$owns (owner: $o, owned: $e) isa owns;
	$r2 (issued: $e, issuer: $j) isa obligation-issuer;
	$risk isa risk;
	$r3 (risk-subject: $j, risk-value: $risk) isa risk-exposure;
}, then {
	(risk-subject: $o, risk-value: $risk) isa risk-exposure;
};

when-risks-then-high-combined-risk sub rule,
when {
	$war isa war, has risk-level 'high';
	$civ-un isa civil-unrest, has risk-level 'high';
	$ter isa terrorism, has risk-level 'high';
	$risk (individual-risk: $war, individual-risk: $civ-un, individual-risk: $ter) isa risk;
}, then {
	$risk has risk-level 'high';
};

when-organisation-withrisk sub rule,
when {
	$risk isa risk-score; $j isa jurisdiction;
	$o isa organisation; $e isa bond;
	$owns (owner: $o, owned: $e) isa owns;
	$r2 (issued: $e, issuer: $j) isa obligation-issuer;
	$r3 (risk-subject: $j, risk-value: $risk) isa risk-exposure;
}, then {
	(risk-value: $risk, risk-subject: $o) isa risk-exposure;
};

when-corp-dir-has-sibling-with-eq sub rule,
when {
	$p isa person; $p2 isa person; $p != $p2;
	$m ($p, $p2) isa mariage;
	$c isa organisation; $bod isa board-of-directors;
	$bm (director: $p, board-member: $bod) isa directorship;
	(managed: $c, company-board: $bod) isa management;
	$o isa obligation;
	$own (owner: $p2, owned: $o) isa owns;
}, then {
	(owner: $c, owned: $o) isa indirect-owns;
};


inferred-high-risk-score sub rule,
when {
	$risk isa risk-score, has value $v;
	$v >= 4.00;
}, then {
	$risk has risk-level 'high';
};

inferred-low-risk-score sub rule,
when {
	$risk isa risk-score, has value $v;
	$v <= 4.00;
}, then {
	$risk has risk-level 'low';
};

inferred-spv-director-owners sub rule,
when {
	$spv isa special-purpose-vehicle;
	$p isa person;
	$o isa obligation;
	$d (board-member: $spv, director: $p) isa directorship;
	$own (owned: $o, owner: $spv) isa owns;
}, then {
	(owner: $p, owned: $o) isa indirect-owns;
};

owns-subsidiary sub rule,
when {
	$b isa bank; $b2 isa bank; $b != $b2;
	$real-estate-corporate isa real-estate-corporate;
	$r1 (owner: $b, owned: $b2) isa owns;
	$r2 (owner: $b2, owned: $real-estate-corporate) isa owns;
},
then {
	(owner: $b, owned: $real-estate-corporate) isa indirect-owns;
	};

bank-cyber-attack sub rule,
when {
	$b isa bank;
	$real-estate-corporate isa real-estate-corporate;
	$attack-campaign isa attack-campaign;
	$r1 (owner: $b, owned: $real-estate-corporate) isa indirect-owns;
	$r2 (attacked: $real-estate-corporate, attacker-campaign: $attack-campaign) isa cyber-attack;
},
then {
	(attacked: $b, attacker-campaign: $attack-campaign) isa cyber-attack;
};

cyber-crime-risk sub rule,
when {
	$b isa bank;
	$attack-campaign isa attack-campaign;
	$cyber-crime isa cyber-crime;
	$r2 (risk-subject: $attack-campaign, risk-value: $cyber-crime) isa risk-exposure;
	$r1 (attacked: $b, attacker-campaign: $attack-campaign) isa cyber-attack;
},
then {
	(risk-value: $cyber-crime, risk-subject: $b) isa risk-exposure;
};


# DATA INSERTS

insert
$b isa bank, has name 'Guolian Securities Co., Ltd', has ticker "1456:HK";
$b2 isa bank, has name 'Royal Bank of Scotland', has ticker "RBS";

$c isa securities-joint-venture, has name "Huaying Securities Co., Ltd.";

(jv-owner: $b, jv-owner: $b2, jv-owned: $c) isa joint-venture;
$j isa jurisdiction, has name 'Lagos', has isoAlpha "NGA", has isoNumeric "566";
$e isa bond, has parValue 0.42, has isin "NI3672572011", has issueTicker "PFARLT", has primary-exchange "Nigerian Stock Exchange", has shares-outstanding 158829816, has voting "Voting", has security-description "Ordinary Shares";
$r2 (issued: $e, issuer: $j) isa obligation-issuer;
$war isa war, has risk-level "high", has value 4.37;
$r3 (risk-subject: $j, risk-value: $war) isa risk-exposure;

$j2 isa jurisdiction, has name 'Jakarta', has isoAlpha "IND", has isoNumeric "316";
$energy-asset isa energy-asset, has name "Akata Oil Field";
$civil isa civil-unrest, has risk-level "high", has value 4.41;
$r12 (owner: $b, owned: $energy-asset) isa owns;
$r13 (contained: $energy-asset, container: $j2) isa located-in;
$r14 (risk-value: $civil, risk-subject: $j2) isa risk-exposure;

$nr isa bank, has name "Nordisk Renting";
$pi isa real-estate-corporate, has name "Posti KiinteistÃ¶t";
$att isa attack-campaign, has name "TPF294";
$ddos isa DDOS, has ddos-type "Protocol Attack";
$cyber-crime isa cyber-crime, has value 4.84;

$b2nr (owner: $b2, owned: $nr) isa owns;
$nrpi (owner: $nr, owned: $pi) isa owns;
$piatt (attacked: $pi, attacker-campaign: $att) isa cyber-attack;
$attddos (attack-used: $att, attack-uses: $ddos) isa attack-type;
$attoscy (risk-subject: $att, risk-value: $cyber-crime) isa risk-exposure;


$woman isa woman, has name "Anna McDonald";
$man isa man, has name "Sheng Chao";
(wife: $woman, husband: $man) isa mariage;

$spv isa special-purpose-vehicle, has name "Cailtin GmbhA";
(director: $woman, board-member: $spv) isa directorship;
(owner: $spv, owned: $e) isa owns;

$spv2 isa special-purpose-vehicle, has name "Avantha GmbhA";
(director: $woman, board-member: $spv2) isa directorship;
$spv3 isa special-purpose-vehicle, has name "Obinto GmbhA";
(director: $woman, board-member: $spv3) isa directorship;
$spv4 isa special-purpose-vehicle, has name "McDougal GmbhA";
(director: $woman, board-member: $spv4) isa directorship;

$zurich isa jurisdiction, has name "Zurich", has isoNumeric "ZCH", has isoNumeric "482";
(container: $zurich, contained: $spv) isa located-in;

$swiss isa country, has name "Switzerland";
(container: $swiss, contained: $spv) isa located-in;

$EU isa continent, has name "Europe";
(container: $EU, contained: $spv) isa located-in;

$bod isa board-of-directors;
(company-board: $bod, managed: $c) isa management;
(director: $man, board-member: $bod) isa directorship;

$man2 isa man, has name "Peter Levine";
(director: $man2, board-member: $bod) isa directorship;
$man3 isa man, has name "Adam Low";
(director: $man3, board-member: $bod) isa directorship;
$man4 isa man, has name "Philip van Dijk";
(director: $man4, board-member: $bod) isa directorship;
$woman5 isa woman, has name "Catherine Jones";
(director: $woman5, board-member: $bod) isa directorship;
$woman6 isa woman, has name "Maria Jose Oritz";
(director: $woman6, board-member: $bod) isa directorship;
$man7 isa man, has name "Boris Cameron";
(director: $man7, board-member: $bod) isa directorship;
$woman8 isa woman, has name "Anamaria Mogg";
(director: $woman8, board-member: $bod) isa directorship;



