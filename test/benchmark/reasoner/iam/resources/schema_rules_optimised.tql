#
# Copyright (C) 2022 Vaticle
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as
# published by the Free Software Foundation, either version 3 of the
# License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.
#

define

rule attribute-parent-company:
    when {
        (company: $c, member: $t) isa company-membership;
        $c has name $c-name;
        $pc isa parent-company-name;
        $c-name = $pc;
    } then {
        $t has $pc;
    };

rule automatic-member-collection:
    when {
        $c isa resource-collection;
        (member: $c) isa collection-membership;
    } then {
        $c has root-collection false;
    };

rule automatic-root-collection:
    when {
        $c isa resource-collection;
        not {
            $c has root-collection false;
        };
    } then {
        $c has root-collection true;
    };

# memberships
inferred-group-membership sub group-membership;
inferred-set-membership sub set-membership;
inferred-collection-membership sub collection-membership;

rule optimised-transitive-membership-groups:
when {
        (group: $p, member: $t) isa! group-membership;
        (group: $t, member: $m) isa group-membership;
} then {
        (group: $p, member: $m) isa  inferred-group-membership;
};

rule optimised-transitive-membership-sets:
when {
        (set: $p, member: $t) isa! set-membership;
        (set: $t, member: $m) isa set-membership;
} then {
        (set: $p, member: $m) isa inferred-set-membership;
};

rule optimised-transitive-membership-collections:
when {
        (collection: $p, member: $t) isa! collection-membership;
        (collection: $t, member: $m) isa collection-membership;
} then {
        (collection: $p, member: $m) isa inferred-collection-membership;
};

# permissions
rule unified-permission-inheritance:
when {
    $access (object: $object, action: $action) isa access;
    $subject isa subject;

    {(group: $granting-subject, member: $subject) isa group-membership;}  or { $granting-subject is $subject; };
    {(set: $granting-action, member: $action) isa set-membership;} or { $granting-action is $action; };
    {(collection: $granting-object, member: $object) isa collection-membership;} or { $granting-object is $object; };

    $granting-access (object: $granting-object, action: $granting-action) isa access;
    (subject: $granting-subject, access: $granting-access) isa direct-permission;
} then {
    (subject: $subject, access: $access) isa inherited-permission;
};

# violations
rule segregation-violation-detection:
    when {
        $po(action: $a1, action: $a2) isa segregation-policy;
        $ac1(object: $o, action: $a1) isa access;
        $ac2(object: $o, action: $a2) isa access;
        $p1(subject: $s, access: $ac1) isa permission;
        $p2(subject: $s, access: $ac2) isa permission;
    } then {
        (subject: $s, object: $o, policy: $po) isa segregation-violation;
    };

rule permission-maximisation-violation-detection:
    when {
        $p-d(subject: $s, access: $ac) isa direct-permission;
        $p-i(subject: $s, access: $ac) isa inherited-permission;
    } then {
        (permission: $p-d, permission: $p-i) isa permission-maximisation-violation;
    };

rule automatic-permission-invalidity:
    when {
        $po(action: $a1, action: $a2) isa segregation-policy;
        $ac1(object: $o, action: $a1) isa access;
        $ac2(object: $o, action: $a2) isa access;
        $p1(subject: $s, access: $ac1) isa permission;
        $p2(subject: $s, access: $ac2) isa permission;
    } then {
        $p1 has validity false;
    };

rule automatic-permission-validity:
    when {
        $p isa permission;
        not {
            $p has validity false;
        };
    } then {
        $p has validity true;
    };
