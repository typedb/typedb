#
# Copyright (C) 2022 Vaticle
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as
# published by the Free Software Foundation, either version 3 of the
# License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.
#

define

company sub entity,
    owns name,
    plays company-membership:company;

company-membership sub relation,
    relates company,
    relates member;

parent-company-name sub attribute,
    value string;

root-collection sub attribute,
    value boolean;

subject sub entity,
    abstract,
    owns parent-company-name,
    owns credential,
    plays company-membership:member,
    plays group-membership:member,
    plays group-ownership:owner,
    plays object-ownership:owner,
    plays permission:subject,
    plays change-request:requester,
    plays change-request:requestee,
    plays segregation-violation:subject;

user sub subject,
    abstract;

user-group sub subject,
    abstract,
    plays group-membership:group,
    plays group-ownership:group,
    plays group-maximisation-violation:group;

object sub entity,
    abstract,
    owns parent-company-name,
    owns object-type,
    plays company-membership:member,
    plays collection-membership:member,
    plays object-ownership:object,
    plays access:object,
    plays segregation-violation:object;

resource sub object,
    abstract;

resource-collection sub object,
    abstract,
    owns root-collection,
    plays collection-membership:collection;

action sub entity,
    abstract,
    owns parent-company-name,
    owns name,
    owns object-type,
    plays company-membership:member,
    plays set-membership:member,
    plays access:action,
    plays segregation-policy:action;

operation sub action;

operation-set sub action,
    plays set-membership:set;

membership sub relation,
    # abstract,
    relates base-parent,
    relates base-member;

group-membership sub membership,
    relates group as base-parent,
    relates member as base-member;

collection-membership sub membership,
    relates collection as base-parent,
    relates member as base-member;

set-membership sub membership,
    relates set as base-parent,
    relates member as base-member;

ownership sub relation,
    # abstract,
    relates base-owned,
    relates base-owner;

group-ownership sub ownership,
    relates group as base-owned,
    relates owner as base-owner,
    owns ownership-type;

object-ownership sub ownership,
    relates object as base-owned,
    relates owner as base-owner,
    owns ownership-type;

access sub relation,
    relates object,
    relates action,
    plays permission:access,
    plays change-request:change;

permission sub relation,
    relates subject,
    relates access,
    owns review-date,
    owns validity,
    plays permission-maximisation-violation:permission;

direct-permission sub permission;
inherited-permission sub permission;

change-request sub relation,
    relates requester,
    relates requestee,
    relates change;

segregation-policy sub relation,
    relates action,
    owns name,
    plays segregation-violation:policy;

violation sub relation,
    abstract;

segregation-violation sub violation,
    relates subject,
    relates object,
    relates policy;

maximization-violation sub violation,
    abstract;

permission-maximisation-violation sub maximization-violation,
    relates permission;

group-maximisation-violation sub maximization-violation,
    relates group;

credential sub attribute,
    value string;

object-type sub attribute,
    value string;

ownership-type sub attribute,
    value string;

review-date sub attribute,
    value datetime;

validity sub attribute,
    value boolean;

person sub user,
    owns full-name,
    owns email;

business-unit sub user-group,
    owns name;

user-role sub user-group,
    owns name;

user-account sub user-group,
    owns email;

file sub resource,
    owns path,
    owns size-kb;

interface sub resource,
    owns name;

record sub resource,
    owns number;

directory sub resource-collection,
    owns path,
    owns size-kb;

application sub resource-collection,
    owns name;

database sub resource-collection,
    owns name;

table sub resource-collection,
    owns name;

id sub attribute,
    abstract,
    value string;

email sub id,
    value string;

name sub id,
    value string;

path sub id,
    value string;

number sub id,
    value string;

full-name sub attribute,
    value string;

size-kb sub attribute,
    value long;

# Fake types & rules for warm-up
warm-up-attribute sub attribute, value string;
warm-up-relation sub relation, relates warm-up, owns warm-up-attribute;
warm-up-attribute plays warm-up-relation:warm-up;

rule warm-up-rule-1:
when { $wr isa warm-up-relation, has warm-up-attribute $wa; }
then { (warm-up: $wa) isa warm-up-relation; };

rule warm-up-rule-2:
when { $wr isa warm-up-relation; $wa isa warm-up-attribute; }
then { $wr has $wa; };
